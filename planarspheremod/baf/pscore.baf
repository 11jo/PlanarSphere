IF
	HPLT(Myself,10)
THEN
	RESPONSE #100
		SetGlobal("psFinalQuestToKillMelCounter","GLOBAL",2)
		ClearAllActions()
		Wait(1)
		StartDialogue("psMelEnd",Player1)
END

IF
	Global("psSpellUp","LOCALS",0)
THEN
	RESPONSE #100
		ReallyForceSpell(Myself,WIZARD_MIRROR_IMAGE)
		ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
		ReallyForceSpell(Myself,WIZARD_GLOBE_OF_INVULNERABILITY)
		ReallyForceSpell(Myself,WIZARD_SPELL_TRAP)
		ReallyForceSpell(Myself,WIZARD_FIRE_SHIELD_RED)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		ReallyForceSpell(Myself,WIZARD_TRUE_SIGHT)
		ReallyForceSpell(Myself,4665)
		SetGlobal("psSpellUp","LOCALS",1)
END

IF
	TookDamage()
	Global("psMelInitialContToggle","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("psMelInitialContToggle","LOCALS",1)
		ReallyForceSpell(Myself,WIZARD_MIRROR_IMAGE)
		ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
		ReallyForceSpell(Myself,WIZARD_GLOBE_OF_INVULNERABILITY)
		ReallyForceSpell(Myself,WIZARD_SPELL_TRAP)
		ReallyForceSpell(Myself,WIZARD_FIRE_SHIELD_RED)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		ReallyForceSpell(Myself,WIZARD_TRUE_SIGHT)		
END

IF
	RandomNum(12,1)
	GlobalLT("psMelCastMantleCounter","LOCALS",6)
THEN
	RESPONSE #100
		ForceSpell(Myself,WIZARD_ABSOLUTE_IMMUNITY)
		ForceSpell(Myself,WIZARD_STONE_SKIN)
		IncrementGlobal("psMelCastMantleCounter","LOCALS",1)
END

IF
	RandomNum(15,1)
	GlobalLT("psMelCastTimestopCounter","LOCALS",4)
	GlobalTimerExpired("psMelTimestopTimer","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("psMelTimestopTimer","LOCALS",18)
		ForceSpell(Myself,WIZARD_TIME_STOP)
		IncrementGlobal("psMelCastTimestopCounter","LOCALS",1)
		Continue()
END

IF
	RandomNum(15,1)
	GlobalLT("psMelCastRageCounter","LOCALS",1)
THEN
	RESPONSE #100
		ForceSpellRes("SPPSMRA",NearestEnemyOf(Myself))
		IncrementGlobal("psMelCastRageCounter","LOCALS",1)
END

IF
	RandomNum(12,1)
	GlobalLT("psMelCastWiltingCounter","LOCALS",4)
THEN
	RESPONSE #100
		ForceSpell(NearestEnemyOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
		IncrementGlobal("psMelCastWiltingCounter","LOCALS",1)
END

IF
	RandomNum(20,1)
	GlobalLT("psMelCastRainCounter","LOCALS",1)
THEN
	RESPONSE #100
		ForceSpellRes("SPPSMELR",NearestEnemyOf(Myself))
		IncrementGlobal("psMelCastRainCounter","LOCALS",1)
END

IF
	RandomNum(20,1)
	GlobalLT("psMelCastOblivionCounter","LOCALS",1)
THEN
	RESPONSE #100
		ForceSpell(Myself,4665)
		IncrementGlobal("psMelCastOblivionCounter","LOCALS",1)
END

IF
	RandomNum(25,1)
	GlobalLT("psMelCastPlanatarCounter","LOCALS",2)
THEN
	RESPONSE #100
		ForceSpell(NearestEnemyOf(Myself),WIZARD_SUMMON_PLANATAR_EVIL)
		IncrementGlobal("psMelCastPlanatarCounter","LOCALS",1)
END

IF
	RandomNum(40,1)
	GlobalLT("psMelCastSpellstrikeCounter","LOCALS",2)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@608)
		ReallyForceSpell(Player1,WIZARD_SPELL_STRIKE)
		ReallyForceSpell(Player2,WIZARD_SPELL_STRIKE)
		ReallyForceSpell(Player3,WIZARD_SPELL_STRIKE)
		ReallyForceSpell(Player4,WIZARD_SPELL_STRIKE)
		ReallyForceSpell(Player5,WIZARD_SPELL_STRIKE)
		ReallyForceSpell(Player6,WIZARD_SPELL_STRIKE)
		IncrementGlobal("psMelCastSpellstrikeCounter","LOCALS",1)
END

IF
	RandomNum(25,1)
	GlobalLT("psMelCastDispelCounter","LOCALS",2)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@609)
		ReallyForceSpell(Player1,WIZARD_TRUE_DISPEL_MAGIC)
		ReallyForceSpell(Player2,WIZARD_TRUE_DISPEL_MAGIC)
		ReallyForceSpell(Player3,WIZARD_TRUE_DISPEL_MAGIC)
		ReallyForceSpell(Player4,WIZARD_TRUE_DISPEL_MAGIC)
		ReallyForceSpell(Player5,WIZARD_TRUE_DISPEL_MAGIC)
		ReallyForceSpell(Player6,WIZARD_TRUE_DISPEL_MAGIC)		
		IncrementGlobal("psMelCastDispelCounter","LOCALS",1)
END

IF
	RandomNum(15,1)
	GlobalLT("psMelCastBigbysCounter","LOCALS",6)
THEN
	RESPONSE #100
		ForceSpell(NearestEnemyOf(Myself),WIZARD_BIGBYS_CRUSHING_HAND)
		IncrementGlobal("psMelCastBigbysCounter","LOCALS",1)
END

IF
	RandomNum(15,1)
	GlobalLT("psMelCastEvilChainComboCounter","LOCALS",4)
THEN
	RESPONSE #100
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_GREATER_MALISON)
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_CHAIN_LIGHTNING)
		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_CHAOS)
		IncrementGlobal("psMelCastEvilChainComboCounter","LOCALS",1)
END

IF
	RandomNum(10,1)
	Range(NearestEnemyOf(Myself),5)
	GlobalLT("psMelCantCatchMeCounter","LOCALS",4)
THEN
	RESPONSE #100
		IncrementGlobal("psMelCantCatchMeCounter","LOCALS",1)
		ReallyForceSpell(Myself,WIZARD_SUN_FIRE)
		Wait(2)
		ReallyForceSpell(Myself,WIZARD_MORDENKAINENS_SWORD)
		ReallyForceSpell(Myself,WIZARD_MORDENKAINENS_SWORD)
		ReallyForceSpell(Myself,WIZARD_INVISIBLE_STALKER)
		ReallyForceSpell(Myself,WIZARD_INVISIBLE_STALKER)
		JumpToPoint([817.769])
END


IF
	RandomNum(10,1)
THEN
	RESPONSE #100
		ForceSpell(NearestEnemyOf(Myself),WIZARD_SUN_FIRE)
END

IF
	RandomNum(5,1)
THEN
	RESPONSE #100
		ForceSpell(NearestEnemyOf(Myself),WIZARD_FIREBALL)
END

IF
	RandomNum(3,1)
THEN
	RESPONSE #100
		ForceSpell(NearestEnemyOf(Myself),WIZARD_CHAIN_LIGHTNING)
END

IF
	RandomNum(2,1)
THEN
	RESPONSE #100
		ForceSpell(NearestEnemyOf(Myself),WIZARD_MAGIC_MISSILE)
END

IF
	RandomNum(2,1)
THEN
	RESPONSE #100
		MoveToObject(Player1)
		Continue()
END